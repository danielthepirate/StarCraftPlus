include "Script/CMSDB_h"


//--------------------------------------------------------------------------------------------------
// Custom Script: IsActiveUser
//--------------------------------------------------------------------------------------------------
bool IsActiveUser (int player) {
	bool active = (PlayerStatus(player) == c_playerStatusActive);
	bool user = (PlayerType(player) == c_playerTypeUser);
	return (active && user);
}
//--------------------------------------------------------------------------------------------------



typedef string[2] paths;

struct CMSDB_SidebarPaths{
	paths builder;
	paths trainer;
	paths researcher;
};

CMSDB_SidebarPaths sidebarPaths;

void CMSDB_SidebarPathsInit(){
	sidebarPaths.builder[0] = "UIContainer/ConsoleUIContainer/BuilderCommandContainer/CommandPanel1";
	sidebarPaths.trainer[0] = "UIContainer/ConsoleUIContainer/TrainerCommandContainer/CommandPanel1";
	sidebarPaths.researcher[0] = "UIContainer/ConsoleUIContainer/ResearcherCommandContainer/CommandPanel1";
	
	sidebarPaths.builder[1] = "UIContainer/ConsoleUIContainer/BuilderCommandContainer/CommandPanel2";
	sidebarPaths.trainer[1] = "UIContainer/ConsoleUIContainer/TrainerCommandContainer/CommandPanel2";
	sidebarPaths.researcher[1] = "UIContainer/ConsoleUIContainer/ResearcherCommandContainer/CommandPanel2";
}

struct CMSDB_PlayerData{
	int sidebar;
	playergroup pg;
	string race;
	unitgroup dummies;
	unitgroup builder;
	unitgroup trainer;
	unitgroup researcher;
	unitgroup idleWorkers;
	unitgroup townhalls;
	
};
CMSDB_PlayerData[c_maxPlayers] playerData;


struct CMSDB_OrderRedirectData{
	int useIdle;
	string abil;
	string dummyCaster;
	string realCaster;
};

typedef structref<CMSDB_OrderRedirectData> CMSDB_OrderRedirect;


trigger CMSDB_ReplaceBuildOrders;

bool CMSDB_OrderRedirectGetFromUserData(CMSDB_OrderRedirect orderdata, string abil){
	string CMDSBOrderRedirectData = "ComandSidebarOrderRedirect";
	orderdata.abil = abil;
	orderdata.useIdle = UserDataGetInt(CMDSBOrderRedirectData, abil, "UseIdle", 1);
	orderdata.dummyCaster = UserDataGetUnit(CMDSBOrderRedirectData, abil, "DummyCaster", 1);
	orderdata.realCaster = UserDataGetUnit(CMDSBOrderRedirectData, abil, "RealCaster", 1);
	return true;
}


bool CMSDB_ReplaceBuildOrders_Func (bool testConds, bool runActions) {
	// Variable Declarations
	unit u;
	unitgroup ug;
	unit dummy;
	point lv_position;
	string abil;
	int player;
	region r;
	order o;
	CMSDB_OrderRedirectData orderdata;
	
	r = RegionEntireMap();
	u = UnitGroupUnit(UnitGroupIdle(1, true), 1);
	dummy = EventUnit();
	player = 1;
	o = EventUnitOrder();
	
	abil = AbilityCommandGetAbility(OrderGetAbilityCommand(o));
	
	CMSDB_OrderRedirectGetFromUserData(orderdata, abil);
	
	if (testConds) {
		if (!((UnitGetType(dummy) == orderdata.dummyCaster))) {
			if (!(UnitHasBehavior(dummy,"SidebarDummy"))) {
			return false;
			}
		}
	}
	
	if(orderdata.useIdle>0){
		ug = UnitGroupIdle(1, true);
		u = UnitGroupUnit(ug, 1);
	}
	else{
		ug = UnitGroup (orderdata.realCaster, player, r, null, 0);
		u = UnitGroupUnit(ug, 1);
	}
	
	if(AbilityClass(abil)==c_classIdCAbilBuild){
		UnitIssueOrder(dummy, Order(AbilityCommand("stop", c_abilStopCmdStop)), c_orderQueueReplace);
		UnitGroupIssueOrder(ug, o, c_orderQueueReplace);
		//UnitIssueOrder(u, o, c_orderQueueReplace);
	}
	else if(AbilityClass(abil)==c_classIdCAbilResearch){
		
		UnitIssueOrder(dummy, Order(AbilityCommand("que5", c_abilQueueCmdCancelLast)), c_orderQueueReplace);
		UnitGroupIssueOrder(ug, o, c_orderQueueReplace);
		//UnitIssueOrder(u, o, c_orderQueueReplace);
	}
	else if(AbilityClass(abil)==c_classIdCAbilTrain){
		
		UnitIssueOrder(dummy, Order(AbilityCommand("que5", c_abilQueueCmdCancelLast)), c_orderQueueReplace);
		UnitGroupIssueOrder(ug, o, c_orderQueueReplace);
		//UnitIssueOrder(u, o, c_orderQueueReplace);
	}
	else if(AbilityClass(abil)==c_classIdCAbilWarpTrain){
		
		UnitIssueOrder(dummy, Order(AbilityCommand("que5", c_abilQueueCmdCancelLast)), c_orderQueueReplace);
		UnitGroupIssueOrder(ug, o, c_orderQueueReplace);
		//UnitIssueOrder(u, o, c_orderQueueReplace);
	}
	
	return true;
}









void CMSDB_AttachDummyToSidebar(unitgroup u, playergroup pg, string sidebar){
	int SideBarDlg = DialogControlHookupStandard(c_triggerControlTypeCommandPanel, sidebar);
	DialogControlSetPropertyAsUnitGroup(SideBarDlg, c_triggerControlPropertyUnitGroup, pg, u);
	DialogControlSetPropertyAsInt(SideBarDlg, c_triggerControlPropertySelectionIndex, pg, 2);
}

unitgroup CMSDB_InitDummies(int player){
	int i = 0;
	int j = 0;
	unitgroup ug;
	unitgroup[2] subgroup;
	string[2] unitid;
	arrayref<paths> path;
	string dummytype;
	
	string CMSDBDummyUserData = "ComandSidebarDummyUnits";
	int TotalItems = UserDataFieldCount (CMSDBDummyUserData);
	point unitPos = RegionGetCenter(RegionEntireMap());

	TriggerDebugOutput(1, StringToText("Player "+IntToString(player)+" Race: "+playerData[player].race), CMSDB_DebugOutput);
	TriggerDebugOutput(1, StringToText("Total Items: "+IntToString(TotalItems)), CMSDB_DebugOutput);
	
	
	for(i = 1;i<=TotalItems;i+=1){
		dummytype = UserDataField(CMSDBDummyUserData, i);
		TriggerDebugOutput(1, StringToText(dummytype), CMSDB_DebugOutput);
		
		if (dummytype == "Builder"){
			ug = playerData[player].builder;
			path = sidebarPaths.builder;
		}
		else if (dummytype == "Trainer"){
			ug = playerData[player].trainer;
			path = sidebarPaths.trainer;
		}
		else if (dummytype == "Researcher"){
			ug = playerData[player].researcher;
			path = sidebarPaths.researcher;
		}
		else if (dummytype == "Warper"){
			continue;
		}
		for(j = 0;j<2;j+=1){
			subgroup[j] = UnitGroupEmpty();
			unitid[j] = UserDataGetUnit(CMSDBDummyUserData, playerData[player].race, dummytype, j+1);
			UnitGroupAddUnitGroup(subgroup[j], UnitCreate(1,unitid[j],c_unitCreateIgnorePlacement, player,unitPos,0.0));
			CMSDB_AttachDummyToSidebar(subgroup[j], playerData[player].pg, path[j]);
			UnitGroupAddUnitGroup(ug,subgroup[j]);
			TriggerDebugOutput(1, StringToText(unitid[j]), CMSDB_DebugOutput);
			TriggerDebugOutput(1, StringToText(path[j]), CMSDB_DebugOutput);
		}
	}
	return ug;
}

void CMSDB_PlayerDataInit(){
	int player = 1;
	string CMSDBDummyUserData = "ComandSidebarDummyUnits";
	string DummyBuilderType;
	point unitPos = RegionGetCenter(RegionEntireMap());
	for(player=0;player < c_maxPlayers;player+=1){
		if (IsActiveUser(player)) {
			playerData[player].race = PlayerRace (player);
			playerData[player].pg = PlayerGroupSingle(player);
			DummyBuilderType = UserDataGetUnit(CMSDBDummyUserData, playerData[player].race, "Builder", 1);
			playerData[player].dummies = UnitGroupEmpty();
			playerData[player].builder = UnitGroupEmpty();
			playerData[player].trainer = UnitGroupEmpty();
			playerData[player].researcher = UnitGroupEmpty();
			
			CMSDB_InitDummies(player);
			
			UnitCreate(2,"Probe",c_unitCreateIgnorePlacement, player,unitPos,0.0);
			UnitCreate(2,"SCV",c_unitCreateIgnorePlacement, player,unitPos,0.0);
			UnitCreate(2,"Drone",c_unitCreateIgnorePlacement, player,unitPos,0.0);
			PlayerModifyPropertyInt (player, c_playerPropMinerals, c_playerPropOperSetTo, 5000);
			PlayerModifyPropertyInt (player, c_playerPropVespene, c_playerPropOperSetTo, 5000);
		}
	}
}


void CMSDB_Init(){
	int i = 0;
	int j = 0;
	string CMDSBOrderRedirectData = "ComandSidebarOrderRedirect";
	int TotalItems = UserDataInstanceCount(CMDSBOrderRedirectData);
	string abil;
	
	
	CMSDB_ReplaceBuildOrders = TriggerCreate("CMSDB_ReplaceBuildOrders_Func");
	CMSDB_SidebarPathsInit();
	CMSDB_PlayerDataInit();
	
	for(i = 1;i<=TotalItems;i+=1){
		abil = UserDataInstance (CMDSBOrderRedirectData, i);
		for(j=0;j<32;j+=1){
			TriggerAddEventUnitOrder(CMSDB_ReplaceBuildOrders, null, AbilityCommand(abil, j));
		}
	}
}

void InitCommandSideBar(){
	CMSDB_Init();
}